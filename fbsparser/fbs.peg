package fbsparser

type Parser Peg {
    Fbs
}

# read https://google.github.io/flatbuffers/flatbuffers_grammar.html

schema <- include end_of_file /
          include statment_decl+ end_of_file/
          statment_decl+ end_of_file

statment_decl <- namespace_decl /
                 union_decl /
                 type_decl /
                 enum_decl /
                 root_decl /
                 file_extension_decl /
                 file_identifier_decl /
                 attribute_decl /
                 rpc_decl /
                 only_comment


namespace_decl <- 'namespace' spacing <[A-z0-9_.\-]+> {p.SetNameSpace(text)} ';' spacing

include <- 'include' spacing ident comment ';' spacing

type_decl <- struct_decl / table_decl

struct_decl <- 'struct' spacing typename spacing metadata* '{' field_decl+ '}' spacing {p.ExtractStruct(false)}

table_decl <- 'table' spacing typename spacing metadata* '{' field_decl+ '}' spacing {p.ExtractStruct(true)}

typename <- ident {p.SetTypeName(text)}

metadata <- '(' <[^)]*> ')'


field_decl <- spacing field_type ':' type metadata* ';' spacing {p.NewExtractField()} /
              spacing field_type ':' type <[ \t]*> '=' <[ \t]*> scalar metadata* ';' spacing {p.NewExtractFieldWithValue()}

field_type <- ident {p.FieldName(text)}

enum_decl <- 'enum' spacing ident spacing metadata* '{' enum_fields '}' spacing /
             'enum' spacing ident ':' type spacing metadata* '{' enum_fields '}' spacing
             
enum_fields <- spacing enum_field ',' /
               spacing enum_field
               

union_decl <- 'union' spacing union_name spacing metadata* '{' enum_fields+ '}' spacing {p.NewUnion()}

union_name <- ident {p.UnionName(text)}

enum_field <- enum_field_type spacing {p.NewExtractField()} /
              enum_field_type spacing '=' spacing integer_constant spacing

enum_field_type <- ident {p.EnumName(text)}

root_decl <- 'root_type' spacing <[A-z0-9_.\-]+> {p.SetRootType(text)} ';' spacing

file_extension_decl <- 'file_extension' <[ \t]*> <[^;]+> ';' spacing 

file_identifier_decl <- 'file_identifier' <[ \t]*> <[^;]+> ';' spacing

attribute_decl <- 'attribute' <[ \t]*> <[^;]+> ';' spacing

rpc_decl <- 'rpc_service' <[ \t]*> ident '{' <[^}]+> '}' spacing

type <- 'bool' spacing {p.SetType("bool")} /
        'int8' spacing {p.SetType("int8")} /
        'uint8' spacing {p.SetType("uint8")} /
        'int16' spacing {p.SetType("int16")} /
        'uint16' spacing {p.SetType("uint16")} /
        'int32' spacing {p.SetType("int32")} /
        'uint32' spacing {p.SetType("uint32")} /
        'int64' spacing {p.SetType("int64")} /
        'uint64' spacing {p.SetType("uint64")} /
        'float32' spacing {p.SetType("float32")} /
        'float64' spacing {p.SetType("float64")} /
        'byte' spacing {p.SetType("uint8")} /
        'ubyte' spacing {p.SetType("uint8")} /
        'short' spacing {p.SetType("int16")} /
        'ushort' spacing {p.SetType("uint16")} /
        'int' spacing {p.SetType("int32")} /
        'uint' spacing {p.SetType("uint32")} /
        'float' spacing {p.SetType("float")} /
        'long' spacing {p.SetType("int64")} /
        'ulong' spacing {p.SetType("uint64")} /
        'double' spacing {p.SetType("float64")} /
        'string' spacing {p.SetRepeated("byte")} /
        ident spacing {p.SetType(text)} /
        '[' type ']' spacing {p.SetRepeated("") }

scalar <- integer_constant / float_constant

integer_constant <- <[0-9]+> / 'true' / 'false'

float_constant <- <[-]*[0-9]+.[0-9]> / float_constant_exp

float_constant_exp <- <[-]*[0-9]+.[0-9]+> <[eE]> <[+-]> <[0-9]+>

ident <- <[a-zA-Z_][a-zA-Z0-9_]*>

# string <- <[A-z0-9_.]+>


only_comment <- spacing ';'
spacing		<- space_comment*
space_comment <- (space / comment)
comment <- '//' (!end_of_line .)* end_of_line
space		<- ' ' / '\t' / end_of_line
end_of_line	<- '\r\n' / '\n' / '\r'
end_of_file	<- !.